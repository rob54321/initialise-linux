#!/usr/bin/perl
use strict;
use warnings;

# this programme exports all debian source packages git
# builds the debian packages, places then in the debian tree, builds the
# Packages file, updates apt-get.
use File::Path;
use File::Basename;
use File::Find;
use Getopt::Std;
use Cwd;
use File::Glob;

# global variables
my ($config_changed, $version, $configFile, $dist, @all_arch, $workingdir, $gitremotepath, $debianroot, $sourcefile);
our ($opt_k, $opt_n, $opt_B, $opt_c, $opt_h, $opt_w, $opt_f, $opt_b, $opt_r, $opt_x, $opt_G, $opt_F, $opt_V, $opt_g, $opt_s, $opt_d, $opt_R);

# sub to get a source tarball and include it in the debian package for building
# if it is required
# the source file is kept in debianroot/source
# The postinst is checked to see if it has SOURCE=source_file, tar or bz2 or tar.gz or tar.bz2
# this is done so the tarball does not have to be included in git
# package name is passed as a parameter. The full directory is
# $workingdir/$packagename
# returns 1 if tarball sucessfully included in package
# returns 2 if no source required, there may or may not be a postinst
# returns 5 if SOURCE is defined but file not found
sub getsource {
	my $package = shift;
	my $postinst = "$workingdir/$package/DEBIAN/postinst";

	# if a source file is to be loaded then postinst will have:
	#SOURCE=sourefile-version.tar.gz | sourcefile-version.tar.bz2 etc
	# return 2 if there is no SOURCE=
	if (open POSTINST, "<", $postinst) {
		#postinst exists, check for SOURCE to find sourcefile name
		while (my $line = <POSTINST>) {
			chomp($line);
			if ($line =~ /^SOURCE=/) {
				# SOURCE found
				$line =~ s/^SOURCE=//;
				# remove ' and/or ". $line now contains filename-version.tar.bz2 or gz or tar
				$line =~ s/"|\'//g;
				$sourcefile = $line;
			}
		} # end while
		close POSTINST;
	} else {
		# there is no postinst and hence no source
		return 2;
	} # end if open
		
	# if version found set the sourcefile name
	if ($sourcefile) {
		# set sourcefile to full path name
		$sourcefile = $debianroot . "/source/" . $sourcefile;
		# check if source file exists, return error otherwise
		return 5 unless -e $sourcefile;
				
		# copy to the source file to $workingdir/$package/tmp
		mkpath "$workingdir/$package/tmp";
		my $copycmd = "cp -f $sourcefile $workingdir/$package/tmp/";
		system($copycmd);
		return 1;	
	} else {
		# there is a postinst but no source required
		return 2;
	} # end if file_version and sourcefile
}

# sub to write config file of parameters that have changed.
# the hash %config contains the key value pairs of the changed variables
# all three vars workingdir, debianroot and gitrepopath are written
# some may not have changed, then the default values are written
# format is variable value
sub writeconfig {
    # set up hash to save
    my %config = ();
    $config{"workingdir"} = $workingdir;
    $config{"debianroot"} = $debianroot;
    $config{"gitrepopath"}    = $gitremotepath;
        
    open OUTFILE, ">$configFile";
    foreach my $item (keys (%config)) {
        print OUTFILE "$item $config{$item}\n";
    }
    close OUTFILE;
}

# sub to get config file if it exists
# if the config file exists, defaults will be read from the file
# if there is no config file the original defaults will be used.
sub getconfig {
	# check if file exists
    	if (open INFILE,"<$configFile") {
			        
		# file format:
		# var_name=value
		# read file into hash and set values
		while (<INFILE>) {
			$workingdir = (split " ", $_)[1] if /workingdir/;
			$debianroot = (split " ", $_)[1] if /debianroot/;
			$gitremotepath = (split " ", $_)[1] if /gitrepopath/;
			}
        # set debi	
		# print a message if any defaults were loaded
		print "loaded config file\n";
		close INFILE;
	}
}
	
# sub to make Packages.gz and Packages.bz2 from the packages file
# architecture must be passed as a parameter
sub makeCompressedPackages {
	my($arch) = $_[0];
	
	# make Packages.gzip Packages.bz2
	my($packagesdir) = $debianroot . "/dists/" . $dist . "/main/binary-" . $arch;
	
	# save current dir
	my($currentdir) = cwd;
	chdir $packagesdir;
	system("gzip -f -k Packages");
	system("bzip2 -f -k Packages");

	# set back to previous dir
	chdir $currentdir;
}

# remove working dir
sub removeworkingdir {
	rmtree $workingdir;
}

# given an archive name this function returns the control field
sub getpackagefield {
	my ($archive, $field) = @_;

	# get the package name from the control file
	my @command = ("dpkg-deb -f ", $archive, $field);
	$field = `@command`;
	chomp $field;

	# check if an error was returned from dpkg-deb
	# the error would be dpkg-deb: error: some description
	if ($field =~ /dpkg-deb: error:/) {
		# file is not a debian package
		print "$field\n";
		return undef;
	} else {
		# return control field from package
		return $field;
	}
}

# first parameter packagename.deb
# all destination directories are created
# destination = debianpool / section / firstchar of archive / packagename
# any architecture is moved.
sub movearchivetotree {
	my($debarchive) = @_;

print "movearchivetotree: $debarchive\n";

	# keep current directory
	my $currentdir = cwd;
	
	# get section, name, version and architecture to create package directory
	my $section = getpackagefield($debarchive, "Section");
	return unless $section;
	my $packagename = getpackagefield($debarchive, "Package");
	return unless $packagename;
	my $version = getpackagefield($debarchive, "Version");
	return unless $version;
	my $architecture = getpackagefield($debarchive, "Architecture");
	return unless $architecture;

	# make dir under pool/firstletter of packagename/packagename
	# get first character of string
	my $firstchar = substr($packagename, 0, 1);

	# make directory
	my $destination = $debianroot . "/pool/" . $section . "/" . $firstchar . "/" . $packagename;
	mkpath($destination);

	# compare the version of the file being inserted to the existing versions
	# in the destination directory for the same architecture.
	# Do not insert an older version, delete and older version in the repository
	# get version of package in the archive
	chdir $destination;
	# make a list of all files with same package name and architecture
	my @repository_files = glob("$packagename*$architecture.deb");

	# There may be multiple files with different versions in the repository
	# if there are two or more files then check that the file being inserted
	# has a version greater than the maximum version
	# set maximum version
	my $max_version = 0;

	# find the maximum version
	foreach my $file_in_repository (@repository_files) {
		# compare versions
		my $version_in_repository = getpackagefield($file_in_repository, "Version");
		$max_version = $version_in_repository if $max_version lt $version_in_repository;
	}
	
	# Insert file to repository if the new version > than the version in the repository
	chdir $currentdir;
	# check if version > max version unless force option is given
	# create a standard archive name for the deb file
	# packagename_version_architecture
	my $debstdarchive = $packagename . "_". $version . "_" . $architecture . ".deb";

	if ($opt_F or ($version gt $max_version)) {
		# delete all previous versions of files in the repository with the same packagename,
		# architecture in the destination
		system("rm -f " . $destination . "/" . $packagename . "*" . $architecture . ".deb");

		# the deb file from subversion or git
		# will be named packagename.deb
		# it must be renamed to packagename_version_architecture.deb
		# an existing deb file must be renamed to standard form
		# ie packagename_version_architecture.deb
		rename $debarchive, $debstdarchive;

		# original was a deb archive, cp it
		print "debpackage: ", $debarchive, " -> $destination/$debstdarchive\n";
		system ("cp " . $debstdarchive . " " . $destination);

		# chmod of file in archive to 0666
		my $pname = $destination . "/" . $debstdarchive;
		chmod (0666, $pname);
	} else {
		# version of new file < existing file
		# file is not inserted
		print "$debarchive = $debstdarchive not inserted $version <= $max_version\n";
	}
}

# add_archive will recursively copy all .deb files to the debian repository
# the package will be renamed to standard form by movetoarchivetree
sub add_archive {
	# get current selection if it is a file
	my $filename = $_;
        
	# for each .deb file, not directory, process it but not in linux-source
	if( -f $filename) {
		# move archive to debian dist tree and create dirs
		if ($filename =~ /\.deb$/) {
			movearchivetotree($filename);
		}
	}
}

# called with buildpackage(workingdirectory, package name
# this function builds a source package(s) exported from git or a debian package 
# into a debian package. The package is then moved to the archive using movetoarchivetree
# movetoarchivetree is called with debian package name and status subversion 
sub buildpackage {
	# get parameters
	my($workdir, $package) = @_;

print "buildpackage: $workdir $package\n";

	# keep current directory
	my $currentdir = cwd;
	
	# change to working directory
	chdir $workdir;

	# build the package and move it to the debian archive
	# check if a package requires a source tarball
	# getsource returns:
	# returns 1 if tarball sucessfully included in package
	# returns 2 if no source required, there may or may not be a postinst
	# returns 5 if source name and version defined but no source file found

	my $gsrc = getsource($package);
	if ($gsrc == 1) {
		print "$package: $sourcefile included\n"

	} elsif ($gsrc == 5) {
		# source and version defined but file not found
		print "$package: $sourcefile defined but not found: skipping\n";
		return;
	}
	# build the package to packagename.deb
	# use deb package name to get full name
	my $rc = system("dpkg-deb -b -Z gzip " . $package . " >/dev/null");
	# check if build was successful
	if ($rc == 0) {
		# the output of dpkg-deb -b is package.deb
		my $debpackage = $package . ".deb";
		movearchivetotree($debpackage);
	} else {
		# control file in DEBIAN directory is not valid or does not exist
		print "control file of $package is not valid\n";
	}
	# restore current directory
	chdir $currentdir;
}

# gitclone is invoked from the -d -g and -n options.
# they require different clone options.
# The options are set my the mode
# mode 1 : invoked by -g  git options: single_branch, depth 1, branch/tag = given by -b
# mode 2 : invoked by -d  git options: single_branch, depth 1, branch = dev
# mode 3 : invoked by -n  git options: no_checkout, all branches download
# gitclone (package_name, mode, targetdirectory)
# the return code from the git clone command is returned.
sub gitclone {
	# get parameters
	my($package, $mode, $workingdir, $branch) = @_;

print "$package $mode $workingdir $branch\n";

	# set options from mode
	my $gitoptions = "";
	if ($mode == 1) {
		$gitoptions = " -v --single-branch --depth=1 -b $branch ";
	} elsif  ($mode == 2) {
		$gitoptions = " -v --single-branch --depth=1 -b dev ";
	} elsif ($mode == 3) {
		$gitoptions = " -v -n ";
	} else {
		# mode is an undefined value, die
		die "mode = $mode is undefined\n";
	}
	
	# project name is project_name.git
	# project_name.git is the repository name
	# remove directory working directory if it exists
	rmtree "$workingdir";
	
	# clone the project	or die if there is an error

print "$gitoptions : $gitremotepath$package.git $workingdir\n";

	my $rc = system("su robert -c 'git clone $gitoptions $gitremotepath$package.git $workingdir >>/tmp/git.log 2>&1' ");
	do {open FH,"<","/tmp/git.log";
		my @gitlog = <FH>;
		close FH;
		print "git.log: @gitlog\n";
		die "gitclone: Error cloning $package\n";
	} unless $rc == 0;

	# remove .git directory
	rmtree $workingdir . "/" . $package . "/.git";

	# remove the readme file and .gitignore
	unlink "$workingdir" . "/" . "$package" . "/README.md";
	unlink "$workingdir" . "/" . "$package" . "/.gitignore";

	return $rc;
}

# sub to get the remote name
# this sub can only be executed in the git cloned directory
sub getremote {
	my @remotelist = `su robert -c 'git remote'`;
	chomp (@remotelist);
	return $remotelist[0];
}


# sub to determine latest commit of latest branch
# and checkout the latest branch
# sub to get latest branch with latest commit checked out
# lbranch no parameters passed or returned
# this sub can only be executed in the git cloned directory
sub lbranch { 
	# get remote name
	my $rname = getremote;
	
	# get heads and sort
	# line 0 will be the latest head, line 1 next etc
	my @line = `su robert -c 'git ls-remote --heads --sort=-committerdate $rname'`;

	# each line contains "commit refs/heads/branch_name"
	# the first line will have the newest date
	# get branch latest branch, it will appear first on the list
	my $lbranch = (split (/\//, $line[0]))[2];
	chomp($lbranch);

	# checkout latest branch so software is available to place in the linux repo
	my $rc = system("su robert -c 'git checkout $lbranch >>/tmp/git.log 2>&1'");
	die ("Could not checkout $lbranch from $rname:$!\n") unless $rc == 0;
	
	# print remote name and latest branch
	print "remote: $rname\t latest branch: $lbranch\n";
}



sub usage {
    print "usage: builddebiantree [options] filelist\
-g [\"pkg\"] extract one package from git with branch or tag given by -b, build->add to tree\
-b branch_or tag name for package given by -g , tags must have been uploaded build->add to tree\
-d [\"pkg1 pkg2 ...\"] extract package from git dev branch, build->add to tree\
-n [\"pkg1 pkg2 ...\"] extract package from git newest branch, build->add to tree\
-r [\"dir1 dir2 ...\"] recurse directory for deb packages list containing full paths, build -> add to archive\
-k show current key id | none if repository not signed
-B [path to debian source tree] builds a debian package and adds to archive\
-F force package to be inserted in tree regardless of version\
-x path, to existing respository, default: $debianroot\
-c path, create a new repository at path
-s scan packages to make Packages\
-G full path of git repo, default: $gitremotepath
-f full path filename to be added\
-w set working directory: $workingdir\
-V print version and exit\
-R reset back to defaults and exit\n";
}

#####################################################
##### main entry #####
#####################################################
# debhomepubkey used for the file debhomepubkey.gpg
my $debhomepubkey = "/etc/apt/keyrings/debhomepubkey.gpg";

# delete logfile
unlink "/tmp/git.log";

# default values
$configFile = "$ENV{'HOME'}/.bdt.rc";
$dist = "home";
@all_arch = ("amd64", "i386", "armhf", "arm64");

# full path to cloned project is
# $workingdir/$package_name
$workingdir = "/tmp/debian";

# the git remote path must be
# appended by /
$gitremotepath = "https://github.com/rob54321/";

$debianroot = "/mnt/debhome";
$sourcefile = undef;
# used for the -r option to work with relative paths
# store the current working directory absolute path
my $initialdir = cwd;

# get config file now, so that command line options
# can override them if necessary
getconfig;

# if no arguments given show usage
my $no_arg = @ARGV;

# check if -b has an argument list after it.
# if not insert default arguments			


# get command line options
getopts('b:n:B:c:FVhkp:r:x:d:sf:w:Rg:G:');


# if no options or h option print usage

if ($opt_h or ($no_arg == 0)) {
	usage;
	# exit
	exit 0;
}
# if -c and -x are not given then use
# the default repository. Make sure it is accessible
if ( ! $opt_c and ! $opt_x ) {
	# check that the default repository is accessible
	die "Cannot access repository at $debianroot" . "/dists/home/main: $!" unless -d $debianroot . "/dists/home/main";
}

# create a new repository
# conflicts with option -x use an existing repository
if ($opt_c) {
	# check that -x is not given
	die "-c and -x are mutually exclusive. Exiting..\n" if $opt_x;

	# the directories must not exist
	if (-d $opt_c) {
		# error message and exit
		print "$opt_c exists: cannot create a repository here\n";
		exit 0;
	} else {
		# set path to use
		$debianroot = $opt_c;

		# strip any tailing / from path
		$debianroot =~ s/\/$//;

		# check if path has leading /
		$debianroot =~ /^\// or die "The repository path: $debianroot is not absolute\n";

		# create the directories
        # make Packages directories if they don't exist
        foreach my $architem (@all_arch) {
          	my $packagesdir = $debianroot . "/dists/" . $dist . "/main/binary-" . $architem;
           	mkpath($packagesdir) if ! -d $packagesdir;
        }
		mkpath($debianroot . "/pool");

        # debhome.sources must be edited with the new url
        #in debhome.sources: URIs: file:///path/to/repo must be
        # changed to URIs: file:///newpath/to/newrepo
        #for sed a file:///mnt/debhome must be used as file:\/\/\/mnt\/debhome
        # each / must be replaced by \/
        my $newdebroot = "file://" . $debianroot;
        $newdebroot =~ s/\//\\\//g;
        system("sed -i -e 's/^URIs:.*/URIs: $newdebroot/' /etc/apt/sources.list.d/debhome.sources");

        # debian root changed, flag it for writing to config file
    	$config_changed = "true";
	}
}

# set up an existing repository to use
# the directory structure must exist
if ($opt_x) {
	# check that -c is not given
	die "-c and -x are mutually exclusive. Exiting..\n" if $opt_c;

	$debianroot = $opt_x;
	# strip any trailing /
	$debianroot =~ s/\/$//;

    # check if path has leading /
    $debianroot =~ /^\// or die "The repository path: $debianroot is not absolute\n";

	if (! -d $debianroot . "/dists/" . $dist . "/main") {
		# directory structure incomplete
		print $debianroot . "/dists/home/main does not exist\n";
		exit 0;
	}

    # debhome.sources must be edited with the new url
    #in debhome.sources: URIs: file:///path/to/repo must be
    # changed to URIs: file:///newpath/to/newrepo
    #for sed a file:///mnt/debhome must be used as file:\/\/\/mnt\/debhome
    # each / must be replaced by \/
    my $newdebroot = "file://" . $debianroot;
    $newdebroot =~ s/\//\\\//g;
    system("sed -i -e 's/^URIs:.*/URIs: $newdebroot/' /etc/apt/sources.list.d/debhome.sources");
	# set flag to say a change has been made
	$config_changed = "true";
}

# print version and exit
if ($opt_V) {
	# print the installed version from dpkg-query
	my $string = `dpkg-query -W builddebiantree`;
	my ($name, $version) = split /\s+/,$string;
	print "Version: $version (installed version)\n";
	exit 0;
}

# set the git repository path if changed
if ($opt_G) {
	$gitremotepath = $opt_G;
	# add a final / to gitrepopath if one does not exist
	$gitremotepath = $gitremotepath . "/" unless $gitremotepath =~ /\/$/;
	
	$config_changed = "true";
}

# set working directory if changed
if ($opt_w) {
    $workingdir = $opt_w;
        
        # set flag to say a change has been made
        $config_changed = "true";
}

# save config file if it has changed
writeconfig if $config_changed;

# export one package from git, branch or tag given by -b, build it and insert into the repository
# export to depth 1 and delete .git directory
# this option will fail if a branch name is not given by -b
if ($opt_g) {
	# if a branch name was not given, die
	die "A branch or tag name must be given with -b\n" unless $opt_b;
	my $branch = $opt_b;
	my $package = $opt_g;

	print "\n";
	print "--------------------------------------------------------------------------------\n";

	gitclone($package, 1, $workingdir . "/" . $package, $branch);
	buildpackage($workingdir, $package);

}

# export a package from git, development branch, build it and insert into the repository
# export to depth 1 and delete .git directory
if ($opt_d) {
	# checkout each package in list input is a space separated string
	my @package_list = split /\s+/, $opt_d;

	foreach my $package (@package_list) {
		print "\n";
		print "--------------------------------------------------------------------------------\n";

	    gitclone($package, 2, $workingdir . "/" . $package, "");
    			
		# build the package and move it to the tree
		buildpackage($workingdir, $package);
	}
}

# export the latest package from git, irrespective of which branch it is on, build it and insert into the repository
# this is the -n newest option
if ($opt_n) {
	# checkout each package in list input is a space separated string
	my @package_list = split /\s+/, $opt_n;

	foreach my $package (@package_list) {
		print "\n";
		print "--------------------------------------------------------------------------------\n";
		# clone the package
	    gitclone($package, 3, $workingdir . "/" . $package, "");
   		# checkout the latest branch
   		chdir $workingdir . "/" . $package;
   		lbranch;
    			
		# build the package and move it to the tree
		buildpackage($workingdir, $package);
	}
}

# process a dir recursively and copy all debian archives to tree
# search each dir for DEBIAN/control. If found build package.
# the opt_r can be a space separated directory list
if ($opt_r) {
	my @directory_list = split /\s+/, $opt_r;
	print "\n";
	print "--------------------------------------------------------------------------------\n";
	foreach my $directory (@directory_list) {
	        # each directory may be an absolute or relative path
	        # absolute paths start with /
	        # relative paths do not start with /
	        # relative paths are relative to the original starting directory
	        # convert all relative paths to absolute paths
	        $directory = $initialdir . "/" . $directory unless $directory =~ /^\//;

	        # check each absolute dir exists
	        print "dir: $directory\n";
    		die "cannot open $directory" if ! -d $directory;

	    	# recurse down dirs and move all
    		find \&add_archive, $directory;
	}
}

# add one specific file to the archive
if ($opt_f) {
	print "\n";
	print "--------------------------------------------------------------------------------\n";
	movearchivetotree($opt_f);			
}
print "\n";
print "--------------------------------------------------------------------------------\n";

# build and add a debian source package to the archive
# the debian source package is not under revision control
# the directory $opt_B will be /home/robert/package
# the debian source tree is under the package directory
# of for relative paths $opt_B may just be the package directory
# in the current directory
if ($opt_B) {
	# empty working directory
	removeworkingdir;

	# strip a trailing /
	$opt_B =~ s/\/$//;;
	
	# make opt_B an absolute directory if it is not
	$opt_B = $initialdir . "/" . $opt_B unless $opt_B =~ /^\//;

	# check that there is a DEBIAN control file
	die ("There is no package source at $opt_B\n") unless -f $opt_B . "/DEBIAN/control";

	# setup package name
	my $package = basename($opt_B);

	# copy the the tree to the working directory
	mkpath($workingdir . "/" . $package) unless -d $workingdir . "/" . $package;
	system("cp -a $opt_B $workingdir/");

	# build the package
	buildpackage($workingdir, $package);

}

# scan pool and make Packages file
if ($opt_s) {

	#change to debian root
	my $currentdir = cwd;
	chdir $debianroot;

	# there is only one distribution ie $debianroot / dists / home 
	# which contains amd64, i386, arm64 and armhf
	# scan all three architectures and write to dists/home/main/binary-arch/Packages
	foreach my $arch (@all_arch) {
		system("apt-ftparchive  --arch " . $arch . " packages pool > dists/" . $dist . "/main/binary-". $arch . "/Packages");

		# make a Packages.gz and Packages.bzip2 in the directory
		makeCompressedPackages($arch);		
		
	}

	# make the release file
	# there is only one release file for all architectures in debianroot/dists/home
	chdir $debianroot . "/dists/" . $dist;
	unlink("Release");
    system("apt-ftparchive -c=/usr/local/bin/apt-ftparchive-home.conf release . > Release");

	# the release file has changed , it must be signed
	# There may be multiple keys in the keyring.
	# /etc/apt/keyrings/debhomepubkey.gpg is always the current key
	# get the current key id using gpg --show-key /etc/apt/keyrings/debhomepubkey.gpg
	# and extract the key id to a file /tmp/currentkeyid

	########################################################
	# output of gpg --show-key
	########################################################
	#pub   ed25519 2026-06-16 [SC] [expires: 2029-06-15]
	#      5D954E8C6641FED507E08E25FB9DC31D1F7D862F
	#uid                      debhome
	#sub   cv25519 2026-06-16 [E]
	########################################################
	
	system("gpg --show-key $debhomepubkey > /tmp/debhomekeyid.txt");
	
	# get the key id
#	$ENV{'GPG_TTY'} = `tty`;
#	chomp $ENV{'GPG_TTY'};	
	
	open (my $fh, "<", "/tmp/debhomekeyid.txt");
	my @keylist = <$fh>;
	close $fh;
	# remove the leading white space from second line
	$keylist[1] =~ s/^\s+//;
	
	# really important to remove the trailing CR/LF
	chomp $keylist[1];
	print "Current key id: $keylist[1]\n";
	system("gpg --batch --yes --pinentry-mode loopback --passphrase  'coahtr3552' --default-key $keylist[1] --clear-sign -o InRelease Release");
	system("gpg --batch --yes -abs --default-key $keylist[1] -o Release.gpg Release");
	# update
	system("apt update");
	
	# restore original directory
	chdir $currentdir;
}

# show the current key id or none if the repository is not signed
if ($opt_k) {
	# get the key id from /etc/apt/keyrings/debhomepubkey.gpg
	# check if /etc/apt/keyrings/debhomebubkey.gpg exists
	if (-f "/etc/apt/keyrings/debhomepubkey.gpg") {
		my @keyinfo = `gpg --show-key /etc/apt/keyrings/debhomepubkey.gpg`;
		# find the line that starts with white space
		for(my $i=0; $i<scalar(@keyinfo); $i++) {
			if ($keyinfo[$i] =~ /^[ ]/) {
				# this line has the key id in it
				# remove the leading white space
				$keyinfo[$i] =~ s/^\s+//;
				chomp($keyinfo[$i]);
				print "Current key id: [$keyinfo[$i]]\n";
				last;
			}
		}
	}
}
